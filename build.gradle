buildscript {
	dependencies {
		classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10")
	}
}

plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.dnd'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

apply plugin: "com.ewerk.gradle.plugins.querydsl"

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	configureEach {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging' //기존 Logback 종속성 제거
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Json 라이브러리
	implementation 'org.json:json:20220320'
	implementation group: 'com.googlecode.json-simple', name:'json-simple', version:'1.1.1'

	// WebSocket
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.webjars:sockjs-client:1.1.2'
	implementation 'org.webjars:stomp-websocket:2.3.3-1'

	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.6' //p6spy (쿼리 로그)
	implementation 'io.springfox:springfox-boot-starter:3.0.0' //Swagger(springfox)
	implementation 'org.springframework.boot:spring-boot-starter-log4j2' //Log4j2
  
	//querydsl 추가
	implementation 'com.querydsl:querydsl-jpa'
	implementation 'com.querydsl:querydsl-apt'

	//UUID
	implementation "com.fasterxml.uuid:java-uuid-generator:4.0.1"

	//AWS S3
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

	//WebClient
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// JAVA JWT
	implementation("com.auth0:java-jwt:3.16.0")

	// Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'

	//MySQL Spatial
	implementation group: 'org.hibernate', name: 'hibernate-spatial', version: '5.6.9.Final'

	//FCM
	implementation group: 'com.google.firebase', name: 'firebase-admin', version: '6.8.1'

	//Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	//Prometheus
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'mysql:mysql-connector-java'
	//Prometheus
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.batch:spring-batch-test'
}

//querydsl 추가
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
	library = "com.querydsl:querydsl-apt"
	jpa = true
	querydslSourcesDir = querydslDir
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', querydslDir]
		}
	}
}

compileQuerydsl{
	options.annotationProcessorPath = configurations.querydsl
}

configurations {
	querydsl.extendsFrom compileClasspath
}

tasks.named('test') {
	useJUnitPlatform()
}